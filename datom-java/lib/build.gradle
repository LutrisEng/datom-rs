// SPDX-FileCopyrightText: 2021 Lutris Engineering, Inc
// SPDX-License-Identifier: BlueOak-1.0.0 OR BSD-2-Clause-Patent
// SPDX-FileContributor: Piper McCorkle <piper@lutris.engineering>

plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

tasks.register('cargo-build', Exec) {
    commandLine 'cargo', 'build'
}

tasks.register('cargo', Copy) {
    dependsOn tasks.named('cargo-build')
    from('../../target/debug') {
        include '*datom_java.so'
        include '*datom_java.dylib'
        include '*datom_java.dll'
    }
    into "${buildDir}/native"
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    // api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    // implementation 'com.google.guava:guava:30.1.1-jre'
}

tasks.named('test') {
    useJUnitPlatform()
    dependsOn tasks.named('cargo')
    systemProperty "java.library.path", file("${buildDir}/native").absolutePath
}

compileJava {
    options.compilerArgs += ["-h", file("${buildDir}/headers")]
}
